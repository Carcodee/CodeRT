#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_scalar_block_layout :enable
#extension GL_EXT_buffer_reference2 : require
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require

#include "../UtilsShaders/ShadersUtility.glsl"

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba8) uniform image2D image;
layout(binding = 2, set = 0) uniform CameraProperties 
{
	mat4 viewInverse;
	mat4 projInverse;
} cam;

struct RayPayload{
    vec3 color;
    vec3 colorLit;
    float distance;
    vec3 normal;
    vec3 origin;
    vec3 direction;
    float roughness;
    float reflectivity;
    bool shadow;
    bool emissive;
    bool isMiss;
};

layout(location = 0) rayPayloadEXT RayPayload rayPayload;

layout (constant_id = 0) const int MAX_RECURSION = 0;

layout(binding=6) uniform light{
    vec3 pos;
    vec3 col;
    float intensity;
}myLight;

layout(set = 0, binding = 10, rgba8) uniform image2D emissiveImage;


layout(push_constant) uniform AccumulatedFrames{
    uint currentFrame;
} pc;

#define SAMPLE_COUNT 5 
#define LIGHT_SOURCES 1 


Material currentMaterial;

void main() 
{

    vec3 result = vec3(0.0f);
    for (uint sampleIndex = 0; sampleIndex < SAMPLE_COUNT; sampleIndex++) {
    
        uvec2 seed = GetSeed(gl_LaunchIDEXT.xy, sampleIndex, pc.currentFrame);   
        
        const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
        const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeEXT.xy);
        vec2 d = inUV * 2.0 - 1.0;
        vec4 origin = cam.viewInverse * vec4(0,0,0,1);
        vec4 target = cam.projInverse * vec4(d.x, d.y, 1, 1) ;
        vec4 direction = cam.viewInverse*vec4(normalize(target.xyz), 0);
        uint rayFlags = gl_RayFlagsOpaqueEXT;
        float tmin = 0.001;
        float tmax = 10000.0;
        
        rayPayload.color = vec3(0.0f);    
        rayPayload.colorLit = vec3(0.0f);    
        rayPayload.origin= origin.xyz;
        rayPayload.shadow = false;
        rayPayload.emissive = false;
        rayPayload.isMiss = false;
        rayPayload.direction = direction.xyz;
        
        vec3 rayThroughput = vec3(1.0f);
        vec3 irradiance = vec3(0.0f);
    
        for (int i = 0; i < MAX_RECURSION; i++) {
            
            traceRayEXT(topLevelAS, gl_RayFlagsOpaqueEXT, 0xff, 0, 0, 0, origin.xyz, tmin, direction.xyz, tmax, 0);
            vec3 directLightContrib = rayPayload.color;
            if(rayPayload.emissive && i==0){
                imageStore(emissiveImage, ivec2(gl_LaunchIDEXT.xy), vec4(directLightContrib, 1.0f));
                break;
            }
            if(rayPayload.shadow && i==0){
                directLightContrib *= 0.3f;
            }
            if(rayPayload.isMiss){
                irradiance += directLightContrib * rayThroughput; 
                break;
            }
            if (rayPayload.distance < 0.0f) {
                rayThroughput *= rayPayload.colorLit; 
                irradiance += directLightContrib * rayThroughput; 
                break;
            }else{ 
               const vec4 hitPos = origin + direction * rayPayload.distance;
               origin.xyz = hitPos.xyz + rayPayload.normal * 0.001f;
               rayPayload.origin=hitPos.xyz;
               vec3 reflectedDir= reflect(direction.xyz, rayPayload.normal);
               vec2 vec2Rand= NextVec2(seed);
               vec3 randomDir =  CosineSampleHemisphere(vec2Rand);
               direction.xyz = GetReflection(reflectedDir, randomDir, rayPayload.roughness);
               rayPayload.direction = direction.xyz;
               
               float lightPdf = 1/LIGHT_SOURCES;
               rayThroughput *= directLightContrib/lightPdf; 
               irradiance += directLightContrib * rayThroughput; 
               
              // irradiance += rayThroughput; 
               
               //if(rand(d.x,d.y)> rayPayload.reflectivity){
               //    direction.xyz = reflectedDir;
               // }
            }

        }
        
        result += irradiance; 
    }
    result = result/SAMPLE_COUNT;
    
    if(pc.currentFrame>0){
        vec3 lastResult = imageLoad(image, ivec2(gl_LaunchIDEXT.xy)).rgb;
        result = (result + pc.currentFrame * lastResult) / (pc.currentFrame + 1.0);
    }
   
    imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(result, 1.0f));
}
