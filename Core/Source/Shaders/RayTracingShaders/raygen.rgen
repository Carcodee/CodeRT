#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_scalar_block_layout :enable
#extension GL_EXT_buffer_reference2 : require
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require

#include "../UtilsShaders/ShadersUtility.glsl"

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba8) uniform image2D image;
layout(binding = 2, set = 0) uniform CameraProperties 
{
	mat4 viewInverse;
	mat4 projInverse;
} cam;


layout(location = 0) rayPayloadEXT RayPayload rayPayload;

layout (constant_id = 0) const int MAX_RECURSION = 0;

layout(binding=6) uniform light{
    vec3 pos;
    vec3 col;
    float intensity;
}myLight;

layout(set = 0, binding = 10, rgba8) uniform image2D emissiveImage;
layout(set = 0, binding = 13, rgba8) uniform image2D AOImage;


layout(push_constant) uniform pushConstantVariables{
    uint currentFrame;
    int minBounceForIndirect;
    float rayTerminationBias;
    float maxVariance;
    float environmentMapIntensity;
    float AOIntensity;
    float AOSize;
} pc;

#define SAMPLE_COUNT 1 
#define LIGHT_SOURCES 1 
#define MAX_IRRADIANCE 8.0f 
#define GAMMA 2.2f

Material currentMaterial;

void main() 
{

    vec3 result = vec3(0.0f);
    vec3 AOResult = vec3(0.0f);
    for (uint sampleIndex = 0; sampleIndex < SAMPLE_COUNT; sampleIndex++) {
    
        uvec2 seed = GetSeed(gl_LaunchIDEXT.xy, sampleIndex, pc.currentFrame);   
        
        const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
        const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeEXT.xy);
        vec2 d = inUV * 2.0 - 1.0;
        vec4 origin = cam.viewInverse * vec4(0,0,0,1);
        vec4 target = cam.projInverse * vec4(d.x, d.y, 1, 1) ;
        vec4 direction = cam.viewInverse * vec4(normalize(target.xyz), 0);
        uint rayFlags = gl_RayFlagsOpaqueEXT;
        float tmin = 0.01;
        float tmax = 1000.0;
        
        rayPayload.color = vec3(0.0f);    
        rayPayload.colorLit = vec3(0.0f);    
        rayPayload.origin= origin.xyz;
        rayPayload.shadow = false;
        rayPayload.emissionColor = vec3(0.0f);
        rayPayload.pdf = 0.0f;
        rayPayload.isMiss = false;
        rayPayload.stop = false;
        rayPayload.direction = direction.xyz;
        rayPayload.sampleDir = direction.xyz;
        rayPayload.hitT = 0.0f;
        rayPayload.frameSeed = seed;
        rayPayload.frame = pc.currentFrame;
        
        vec3 rayThroughput = vec3(1.0f);
        vec3 irradiance = vec3(0.0f);
        float rayPdf = 1.0f;
        bool sampleShadowed = false;
        float aoSample = 1.0f;
    
        for (int i = 0; i < MAX_RECURSION; i++) {
            
            traceRayEXT(topLevelAS, 0, 0xff, 0, 0, 0, origin.xyz, tmin, direction.xyz, tmax, 0);
            vec3 directLightContrib = rayPayload.color;
            
            if(rayPayload.isMiss){
                irradiance += (directLightContrib * pc.environmentMapIntensity) * rayThroughput; 
                if(sampleShadowed){
                    irradiance *= 0.7f;   
                }
                break;
            }
            if(rayPayload.hitT <= pc.AOSize && i == 1 && !rayPayload.isMiss){
                aoSample = (rayPayload.hitT/pc.AOSize)+MaxComponent(directLightContrib);
                aoSample = min(1.0f, aoSample);
            }
            
            irradiance += rayPayload.emissionColor * rayThroughput ; 
            if(i==0){
                imageStore(emissiveImage, ivec2(gl_LaunchIDEXT.xy), vec4(rayPayload.emissionColor, 1.0f));
            }
            
            if(rayPayload.shadow && i==0){
                sampleShadowed = true;
            }
            
            if(rayPayload.pdf < EPSILON || IsBlack(rayPayload.colorLit)){
                break;
            }
            if(rayPayload.stop){
                break;
            }
            
            irradiance += directLightContrib * rayThroughput / rayPdf; 
            const vec4 hitPos = origin + direction * rayPayload.distance;
            origin.xyz = hitPos.xyz + rayPayload.normal * 0.001f;
            direction.xyz = rayPayload.sampleDir;
            rayThroughput *= rayPayload.colorLit; 
            rayPdf *= rayPayload.pdf;  
            
            if(i >= pc.minBounceForIndirect){
                float threshold = max(0.05f,1.0f-MaxComponent(rayThroughput));
                if(threshold > NextFloat(seed) + pc.rayTerminationBias){
                   break; 
                }
                rayThroughput /= 1.0f - threshold;
                rayThroughput = min(vec3(pc.maxVariance), rayThroughput);
            
            }
        }
        AOResult += vec3(aoSample);
        result += min(irradiance, MAX_IRRADIANCE); 
    }
    result = result/SAMPLE_COUNT;
    result = pow(result, vec3(1.0/GAMMA));
    
    AOResult = AOResult/SAMPLE_COUNT;
    
    if(pc.currentFrame>0){
        vec3 lastAOResult = imageLoad(AOImage, ivec2(gl_LaunchIDEXT.xy)).rgb;
        AOResult = (AOResult + pc.currentFrame * lastAOResult) / (pc.currentFrame + 1.0);
        
        vec3 lastResult = imageLoad(image, ivec2(gl_LaunchIDEXT.xy)).rgb;
        result = (result + pc.currentFrame * lastResult) / (pc.currentFrame + 1.0);
    }
   
    imageStore(AOImage, ivec2(gl_LaunchIDEXT.xy), vec4(AOResult, 1.0f));
    imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(result, 1.0f));
}
